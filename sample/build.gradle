import org.gauge.gradle.GaugeTaskNew

plugins {
    id 'java'
    id 'jvm-test-suite'
    id 'org.gauge'
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    spec {
        java.srcDirs('src/spec/')
    }
    test {
        compileClasspath += sourceSets.spec.runtimeClasspath
        runtimeClasspath += sourceSets.spec.runtimeClasspath
    }
}

dependencies {
    specImplementation 'com.thoughtworks.gauge:gauge-java:+'

    testImplementation 'com.thoughtworks.gauge:gauge-java:+'
    testImplementation("org.junit.jupiter:junit-jupiter:5.+")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

tasks.register('cleanBuildDir', Delete) {
    delete "${projectDir}/logs"
    delete "${projectDir}/out"
    delete "${projectDir}/reports"
}
tasks.clean.dependsOn("cleanBuildDir")

gauge {
    specsDir = 'specs'
    inParallel = true
    nodes = 4
    env = 'ci'
    additionalFlags = '--verbose'
}

tasks.register('gaugeDev', GaugeTaskNew) {
    doFirst {
        gauge {
            specsDir = 'specs'
            inParallel = true
            nodes = 2
            env = 'dev'
            additionalFlags = '--verbose'
        }
    }
}

tasks.register('gaugeDevRepeat', GaugeTaskNew) {
    dependsOn("gaugeDev")
    doFirst {
        gauge {
            additionalFlags = '--repeat --simple-console'
        }
    }
}

tasks.register('gaugeTest', GaugeTaskNew) {
    doFirst {
        gauge {
            specsDir = 'specs'
            inParallel = true
            nodes = 4
            env = 'test'
            additionalFlags = '--verbose'
        }
    }
}

tasks.register('gaugeCi', GaugeTaskNew) {
    doFirst {
        gauge {
            specsDir = 'specs'
            inParallel = true
            nodes = 3
            env = 'ci'
            additionalFlags = '--verbose'
        }
    }
}

tasks.withType(Test).configureEach {
    // https://docs.gradle.org/8.2/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
    // Using JUnitPlatform for running tests
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

tasks.withType(GaugeTaskNew).configureEach {
    // https://docs.gradle.org/8.2/userguide/upgrading_version_8.html#test_task_default_classpath
    testClassesDirs = sourceSets.test.output
    classpath = sourceSets.test.runtimeClasspath
}